plugins {
    id 'io.spring.dependency-management' version '0.6.1.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.3'
}

apply plugin: 'java-library'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

group 'com.4dconcept.springframework.data'
version '1.1.0.BUILD-SNAPSHOT'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    jcenter()
    maven { url "http://developer.marklogic.com/maven2" }
}

dependencyManagement {
    imports {
        mavenBom("io.spring.platform:platform-bom:${springBomVersion}")
    }
}

dependencies {
    api 'org.springframework.data:spring-data-commons'

    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.marklogic:marklogic-xcc:8.0.3'
    implementation 'javax.transaction:javax.transaction-api'
    implementation 'org.aspectj:aspectjweaver'
    implementation 'org.springframework:spring-context'
    implementation 'org.springframework:spring-oxm'
    implementation 'org.springframework:spring-tx'

    testImplementation 'ch.qos.logback:logback-classic'
    testImplementation 'junit:junit'
    testImplementation 'org.hamcrest:hamcrest-core'
    testImplementation 'org.hamcrest:hamcrest-library'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.springframework:spring-test'
}

def pomConfig = {
    organization {
        name '4DConcept'
        url 'http://www.4dconcept.com/en'
    }
    issueManagement {
        system 'GitHub Issues'
        url 'https://github.com/stoussaint/spring-data-marklogic/issues'
    }
    scm {
        url "https://github.com/stoussaint/spring-data-marklogic"
        connection "scm:git:git://github.com/stoussaint/spring-data-marklogic.git"
        developerConnection "scm:git:git@github.com:stoussaint/spring-data-marklogic.git"
    }
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'stoussaint'
            name 'StÃ©phane Toussaint'
            email 'stephane.toussaint@4dconcept.fr'
        }
        developer {
            id 'sktifa'
            name 'Sahbi Ktifa'
            email 'sahbi.ktifa@4dconcept.fr'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

test {
    exclude '**/*IntegrationTests.class'
}

task integrationTest( type: Test ) {
    include '**/*IntegrationTests.class'
}

asciidoctor {
    attributes \
        'build-gradle' : file('build.gradle'),
            'source-highlighter': 'coderay',
            'imagesdir': 'images',
            'doctype': 'book',
            'toc': '',
            'toc-placement!': '',
            'toclevels': '2',
            'icons': 'font',
            'setanchors': '',
            'idprefix': '',
            'idseparator': '-',
            'docinfo1': '',
            'spring-data-commons-docs':file('../spring-data-commons/src/main/asciidoc'),
            'spring-data-marklogic-source':file('src/main/java')
}
